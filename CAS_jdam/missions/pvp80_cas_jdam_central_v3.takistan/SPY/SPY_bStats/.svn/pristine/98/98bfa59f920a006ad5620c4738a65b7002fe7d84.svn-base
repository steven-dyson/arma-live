/***************************************************************************
VEHICLE TEAMKILL
Created by Spyder
spyder@armalive.com
****************************************************************************/

private ["_killerName", "_killerUID", "_kVarName", "_vehicle", "_weapon", "_distance", "_killerPos", "_vehiclePos", "_addValue", "_addPunish", "_score", "_addScore", "_currentScore", "_award", "_fundsName"];

_killerName = (_this select 0);
_killerUID = (_this select 1);
_kVarName = (_this select 2);
_vehicle = (_this select 3);
_weapon = (_this select 4);
_distance = (_this select 5);
_killerPos = (_this select 6);
_vehiclePos = (_this select 7);

// Player Message
if ((SPY_bStats_msgsEnabled)) then {

	_null = [5, (format ["%1 destroyed a friendly %2 (%3)", _killerName, ([_vehicle] call SPY_displayName), ([_weapon] call SPY_displayName)]), "CLIENT", false] spawn SPY_bMessage;

};

// Fail safe, need to figure out why this is needed
if ((_kVarName == "")) then {_kVarName = (format ["SPY_bStats_%1", _killerUID]);};

// Current value increased by 1
_addValue = ((SPY_container getVariable _kVarName select 6) + 1);

// Add score
_score = (["vehtk", objNull, _addValue] call SPY_scoreValue);
_addScore = ((SPY_container getVariable _kVarName select 0) + _score);

// All current values in the array
_currentScore = (SPY_container getVariable _kVarName);

// Set increased value in array
_currentScore set [0, _addScore];
_currentScore set [6, _addValue];

// Broadcast array
SPY_container setVariable [_kVarName, _currentScore, true];

// Format/ Send A2U Info
[(format ["bstats_vehtk (%1, %2, %3, %4, %5, %6, %7, %8)", (str (typeOf _vehicle)), str _killerUID, time, (str _weapon), _score, _distance, (str str _killerPos), (str str _vehiclePos)])] call uplink_exec;