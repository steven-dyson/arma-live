/***************************************************************************
Aircraft Crash
Created by Spyder
spyder@armalive.com
***************************************************************************/

private ["_vUID", "_vName", "_vPos", "_vehicle", "_vSide", "_vehicleName", "_crewNumber", "_vScoreVar", "_currentScore", "_score", "_addScore", "_addDeath", "_addCrash"];

_vUID = _this select 0;
_vName = _this select 1;
_vPos = _this select 2;
_vehicle = _this select 3;
_vSide = _this select 4;

_vehicleName = ([_vehicle] call SPY_displayName);

// Number of players in vehicle (minus pilot)
_crew = (_vehicle getVariable "SPY_VEHICLE_INFO" select 1);
_crewNumber = ((count _crew) - 1);

// Aircraft not initialized
if ((isNil "_crewNumber")) then {_crewNumber = 0;};

// Player message
if ((SPY_bStats_msgsEnabled)) then
{
	_null = [[5, (format ["%1 crashed their %2 (%3 passengers)", _vName, _vehicleName, _crewNumber]), 0, ["SPY bStats", "Event Log"], false], "SPY_bMessage", true, false, false] call BIS_fnc_MP;
};

// All current values in the array
_vScoreVar = (format ["SPY_bStats_%1", _vUID]);
_currentScore = (SPY_container getVariable _vScoreVar);

//Add score
_score = (["accrash", objNull, _crewNumber] call SPY_scoreValue);
_addScore = ((_currentScore select 0) + _score);

// Current value increased by 1
_addDeath = ((_currentScore select 4) + 1);
_addCrash = ((_currentScore select 9) + 1);

// Set increased value in array
_currentScore set [0, _addScore];
_currentScore set [4, _addDeath];
_currentScore set [9, _addCrash];

switch (_vSide) do {

	case WEST: {[[0, 1, 0, 0]] call SPY_addSideScore;};
	case EAST: {[[0, 0, 0, 1]] call SPY_addSideScore;};
	
};

// Broadcast array
SPY_container setVariable [_vScoreVar, _currentScore, true];

// Send to armalive
"armalive" callExtension format
[
	"accrash1;%1;%2;%3;%4;%5;%6",
	time,
	_vUID,
	_vPos,
	_crewNumber,
	(typeOf _vehicle),
	_vPos
];