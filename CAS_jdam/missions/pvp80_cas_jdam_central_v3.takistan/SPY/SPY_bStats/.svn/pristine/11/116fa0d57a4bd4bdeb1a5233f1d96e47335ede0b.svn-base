/***************************************************************************
Add Vehicle
Created by Spyder
spyder@armalive.com
****************************************************************************/



/***************************************************************************
INIT
****************************************************************************/
private ["_vehicle", "_switched", "_vehicleInfo", "_vehicleSide", "_playerInfo", "_playerSide", "_playerUID"];

_vehicle = (_this select 0);
_switched = (_this select 1);

if ((isNil {_vehicle getVariable "SPY_VEHICLE_INFO"})) then {

	waitUntil {sleep 0.1; (!isNil {_vehicle getVariable "SPY_VEHICLE_INFO"})};
	
};

_vehicleInfo = (_vehicle getVariable "SPY_VEHICLE_INFO");
_vehicleSide = (_vehicleInfo select 0);

_playerInfo = (player getVariable "SPY_PLAYER_INFO");
_playerSide = (player getVariable "SPY_id_player" select 2);
_playerUID = (player getVariable "SPY_id_player" select 0);
/***************************************************************************
END
****************************************************************************/



/***************************************************************************
SET PLAYER VEHICLE INFO
****************************************************************************/
_playerInfo set [2, true];
if (((driver _vehicle) == player) || (_switched)) then {_playerInfo set [3, true];};
_playerInfo set [4, _vehicle];
/***************************************************************************
END
****************************************************************************/



/***************************************************************************
SET VEHICLE SIDE INFO & RESET DAMAGING UNIT
****************************************************************************/
_vehicleInfo set [0, _playerSide];
_vehicleInfo set [1, (crew _vehicle)];

if ((_vehicleSide != _playerSide)) then {
	
	_null = [[_vehicle], "_null = _this spawn SPY_resetDamageVehicle;", "SERVER"] spawn CAS_mpCB;

	_vehicleInfo set [2, true];
	_vehicle setVariable ["SPY_VEHICLE_INFO", _vehicleInfo, true];
	
	sleep 10;
	
	_vehicleInfo set [2, false];
	_vehicle setVariable ["SPY_VEHICLE_INFO", _vehicleInfo, true];

};
/***************************************************************************
END
****************************************************************************/