/***************************************************************************
KILL ASSIST
Created by Spyder
spyder@armalive.com
***************************************************************************/

private ["_unitUID", "_unitScoreVar", "_addAssist", "_score", "_addScore", "_unitScore"];

_unitUID = (_this select 0);

// Player Message
if ((SPY_bStats_msgsEnabled) && (SPY_bStats_msgsEnemy)) then {

	_null = [5, "You got a kill assist", _unitUID, false] spawn SPY_bMessage;

};

_unitScoreVar = (format ["SPY_bStats_%1", _unitUID]);

// All current values in the array
_unitScore = (SPY_container getVariable _unitScoreVar);

// Add score
_score = (["killassist", objNull, 0] call SPY_scoreValue);
_addScore = ((_unitScore select 0) + _score);

// Current value increased by 1
_addAssist = ((_unitScore select 8) + 1);

// Set increased value in array
_unitScore set [0, _addScore];
_unitScore set [8, _addAssist];

// Broadcast array
SPY_container setVariable [_unitScoreVar, _unitScore, true];

// Format/ Send A2U Info
[(format ["bstats_killassist (%1, %2, %3)", (str _unitUID), time, _score])] call uplink_exec;